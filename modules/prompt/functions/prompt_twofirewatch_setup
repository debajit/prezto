#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_twofirewatch_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_twofirewatch_colors[1]}%b%f%u%c${_prompt_twofirewatch_colors[4]}●%f)"
  else
    branch_format="(${_prompt_twofirewatch_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_twofirewatch_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_twofirewatch_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_twofirewatch_colors=(
      "%F{109}" # 1 Turquoise
      "%F{166}" # 2 Orange
      "%F{135}" # 3 Purple
      "%F{197}" # 4 Hotpink
      "%F{118}" # 5 Limegreen
      "%F{168}" # 6 Pink (Two-Firewatch)
      "%F{67}"  # 7 Blue (Two-Firewatch)
      "%F{238}" # 8 Grey (Two-Firewatch)
      "%F{138}" # 9 Reddish cream (Two-Firewatch)
      "%F{7}"   # 10 Reset color (white)
      "%F{95}"  # 11 Greyish dark reddish
      "%F{203}" # 12 Bright orange (Two Firewatch)
    )
  else
    _prompt_twofirewatch_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="${_prompt_twofirewatch_colors[1]}%b%f%u%c"
  local action_format="${_prompt_twofirewatch_colors[5]}%a%f"
  local unstaged_format="${_prompt_twofirewatch_colors[4]}●%f"
  local staged_format="${_prompt_twofirewatch_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format} ${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${_prompt_twofirewatch_colors[7]}%n%f ${_prompt_twofirewatch_colors[8]}at ${_prompt_twofirewatch_colors[9]}%m%f ${_prompt_twofirewatch_colors[8]}in ${_prompt_twofirewatch_colors[6]}%3d%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"${_prompt_twofirewatch_colors[11]}● %f"
  RPROMPT=''
}

prompt_twofirewatch_setup "$@"
